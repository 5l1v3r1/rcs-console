<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009"
               xmlns:s="library://ns.adobe.com/flex/spark"
               xmlns:mx="library://ns.adobe.com/flex/mx"
               width="480" close="PopUpManager.removePopUp(this)" creationComplete="init()"
               title="{R.get('HEALTH_CHECK')}">
  
  <s:layout>
    <s:VerticalLayout gap="40" horizontalAlign="center" paddingBottom="10" paddingLeft="10"
                      paddingRight="10" paddingTop="40"/>
  </s:layout>

  <fx:Script>
    <![CDATA[
      import locale.R;
      
      import mx.collections.ArrayCollection;
      import mx.managers.PopUpManager;
      import mx.rpc.events.FaultEvent;
      import mx.rpc.events.ResultEvent;
      import mx.rpc.http.mxml.HTTPService;
      
      [Bindable]
      public var nodes:ArrayCollection;
      
      private function init():void
      {
        var entries:ArrayCollection = new ArrayCollection();
        for each (var o:Object in nodes)
        entries.addItem(new HealthCheckEntry(o));
        grid.dataProvider = entries;
      }
      
      private var currentIndex:int;
      private var currentEntry:HealthCheckEntry;
      private function start():void
      {
        checkButton.enabled = false;
        currentIndex = -1;
        if (grid.dataProvider.length > 0) {
          for each (var entry:HealthCheckEntry in grid.dataProvider)
            entry.status = HealthCheckEntry.IDLE;
          checkNext();
        }
      }
      
      private function checkNext():void
      {
        currentIndex++;
        if (grid.dataProvider.length > currentIndex)
          check(currentIndex);
        else
          checkButton.enabled = true;
      }
      
      private function check(index:int):void
      {
        var entry:HealthCheckEntry = grid.dataProvider.getItemAt(index) as HealthCheckEntry;
        currentEntry = entry;
        currentEntry.status = HealthCheckEntry.CHECKING;
        trace('--- checking ' + currentEntry.ip + ' ' + currentEntry.name);
        
        var http:HTTPService = new HTTPService();
        http.url = 'http://'+entry.ip+':8080';
        http.requestTimeout = 3;
        http.addEventListener(ResultEvent.RESULT, manageResult);
        http.addEventListener(FaultEvent.FAULT, manageFault);
        http.send();
      }
      
      private function manageResult(e:ResultEvent):void
      {
        trace('    result ' + currentEntry.name);
        currentEntry.status = HealthCheckEntry.OK;
        grid.invalidateDisplayList();
        checkNext();
      }
      
      private function manageFault(e:FaultEvent):void
      {
        trace('    fault ' + currentEntry.name);
        currentEntry.status = HealthCheckEntry.KO;
        grid.invalidateDisplayList();
        checkNext();
      }
      
    ]]>
  </fx:Script>
  
  <s:Label text="{R.get('HEALTH_CHECK_TEXT')}"/>
  
  <s:Button id="checkButton" height="40" label="Start Configuration Check" click="start()"
            fontSize="22"/>

  <s:DataGrid id="grid" width="100%" height="207">
    <s:columns>
      <s:ArrayList>
        <s:GridColumn dataField="name" headerText="Name"/>
        <s:GridColumn dataField="ip" headerText="Address"/>
        <s:GridColumn dataField="status" headerText="Status"
                      itemRenderer="it.ht.rcs.console.network.view.collectors.healthCheck.StatusRenderer"/>
      </s:ArrayList>
    </s:columns>
  </s:DataGrid>

</s:TitleWindow>