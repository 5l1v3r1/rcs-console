<?xml version="1.0" encoding="utf-8"?>
<s:DefaultGridHeaderRenderer xmlns:fx="http://ns.adobe.com/mxml/2009"
                             xmlns:s="library://ns.adobe.com/flex/spark"
                             xmlns:mx="library://ns.adobe.com/flex/mx"
                             click="clickHandler(event)"
                             addedToStage="init()" removedFromStage="destroy()">
  
  <fx:Script>
    <![CDATA[
      import mx.managers.PopUpManager;
      
      import spark.components.CheckBox;
      import spark.layouts.HorizontalLayout;
      import spark.layouts.VerticalAlign;
      
      [Bindable]
      public var _popup:AbstractFilterPopup;
      public static var _currentPopup:AbstractFilterPopup;
      public var popup:IFactory;
      
      [Bindable]
      public var mandatory:Boolean = false;
      [Bindable]
      public var filter:Object;
      
      private function init():void {
        _popup = popup.newInstance();
        _popup.property = column.dataField;
        PopUpManager.addPopUp(_popup, this, false);
        if (mandatory) showFilter.selected = true;
      }
      
      private function destroy():void {
        PopUpManager.removePopUp(_popup);
      }
      
      override public function prepare(hasBeenRecycled:Boolean):void {
        super.prepare(hasBeenRecycled);
        labelDisplayGroup.addElementAt(showFilter, 0);
        var layout:HorizontalLayout = new HorizontalLayout();
        layout.verticalAlign = VerticalAlign.MIDDLE;
        layout.gap = 0;
        labelDisplayGroup.layout = layout;
      }
      
      override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void {
        super.updateDisplayList(unscaledWidth, unscaledHeight);
        if (_popup && _popup.visible) centerPopup();
      }
      
      private function centerPopup():void {
        var center:Point = localToGlobal(new Point(x, y));
        _popup.x = center.x - x;
        _popup.y = center.y + height;
        if (_popup.x + _popup.width > owner.width)
          _popup.x = center.x - x - _popup.width + width + 1;
      }
      
      private function clickHandler(event:MouseEvent):void {
        
        if (_currentPopup && _currentPopup.visible) {
          _currentPopup.visible = false;
          if (_currentPopup === _popup) {
            _currentPopup = null;
            return;
          }
        }

        centerPopup();
        _popup.visible = true;
        _currentPopup = _popup;
        
      }
      
      private function changeHandler(event:Event):void {
        _popup.resetFilter();
        _popup.visible = false;
      }
      
    ]]>
  </fx:Script>

  <fx:Declarations>
    <s:CheckBox id="showFilter" change="changeHandler(event)"
                enabled="{!mandatory &amp;&amp; showFilter.selected}"
                selected="{mandatory || _popup.valid}"/>
  </fx:Declarations>

</s:DefaultGridHeaderRenderer>